INTR_IVADD EQU 003CH ;INTR 对应的中断矢量地址
INTR_OCW1 EQU 0A1H ;INTR 对应 PC 机内部 8259 的 OCW1 地址
INTR_OCW2 EQU 0A0H ;INTR 对应 PC 机内部 8259 的 OCW2 地址
INTR_IM EQU 0FBH ;INTR 对应的中断屏蔽字
IOY0 EQU 0E000H ;片选 IOY0 对应的端口始地址
MY8259_ICW1 EQU IOY0+00H ;实验系统中 8259 的 ICW1 端口地址
MY8259_ICW2 EQU IOY0+04H ;实验系统中 8259 的 ICW2 端口地址
MY8259_ICW3 EQU IOY0+04H ;实验系统中 8259 的 ICW3 端口地址
MY8259_ICW4 EQU IOY0+04H ;实验系统中 8259 的 ICW4 端口地址
MY8259_OCW1 EQU IOY0+04H ;实验系统中 8259 的 OCW1 端口地址
MY8259_OCW2 EQU IOY0+00H ;实验系统中 8259 的 OCW2 端口地址
MY8259_OCW3 EQU IOY0+00H ;实验系统中 8259 的 OCW3 端口地址
STACK1 SEGMENT STACK
 DW 256 DUP(?)
STACK1 ENDS
DATA SEGMENT 
MES DB 'Press any key to exit!',0AH,0DH,0AH,0DH,'$'
CS_BAK DW ? ;保存 INTR 原中断处理程序入口段地址的变量
IP_BAK DW ? ;保存 INTR 原中断处理程序入口偏移地址的变量
IM_BAK DB ? ;保存 INTR 原中断屏蔽字的变量
DATA ENDS
CODE SEGMENT
 ASSUME CS:CODE,DS:DATA, SS:STACK1
START: MOV AX,DATA
 MOV DS, AX
 MOV DX,OFFSET MES
 MOV AH, 09H ;显示退出提示
 INT 21H
 CLI
 MOV AX, 0000H 
 MOV ES, AX ;替换 INTR 的中断矢量
 MOV DI, INTR_IVADD ;保存 INTR 原中断处理程序入口偏移地址 
 MOV AX, ES:[DI]
 MOV IP_BAK,AX
 MOV AX, OFFSET MYISR ;设置当前中断处理程序入口偏移地址
 MOV ES:[DI],AX 
 ADD DI, 2
 MOV AX, ES:[DI] ;保存 INTR 原中断处理程序入口段地址
 MOV CS_BAK,AX
 MOV AX, SEG MYISR ;设置当前中断处理程序入口段地址
 MOV ES:[DI],AX 
 MOV DX,INTR_OCW1 ;设置中断屏蔽寄存器，打开 INTR 的屏蔽位
 IN AL, DX ;保存 INTR 原中断屏蔽字
 MOV IM_BAK,AL 
 AND AL, INTR_IM ;允许 PC 机内部 8259 的 IR2 中断
 OUT DX,AL
 MOV DX, MY8259_ICW1 ;初始化实验系统中 8259 的 ICW1
 MOV AL, 13H ;边沿触发、单片 8259、需要 ICW4
 OUT DX,AL
 MOV DX, MY8259_ICW2
 MOV AL, 08H ;初始化实验系统中 8259 的 ICW2
 OUT DX,AL
 MOV DX, MY8259_ICW4 ;初始化实验系统中 8259 的 ICW4
 MOV AL, 01H ;非自动结束 EOI
 OUT DX,AL
 MOV DX, MY8259_OCW3 ;向 8259 的 OCW3 发送读取 IRR 命令
 MOV AL, 0AH
 OUT DX,AL 
 MOV DX, MY8259_OCW1 ;初始化实验系统中 8259 的 OCW1
 MOV AL, 0FCH ;打开 IR0 和 IR1 的屏蔽位
 OUT DX,AL
 STI 
WAIT1: MOV AH,1 ;判断是否有按键按下
 INT 16H
 JZ WAIT1 ;无按键则跳回继续等待，有则退出
QUIT: CLI
 MOV AX, 0000H ;恢复 INTR 原中断矢量
 MOV ES, AX
 MOV DI, INTR_IVADD ;恢复 INTR 原中断处理程序入口偏移地址
 MOV AX, IP_BAK
 MOV ES:[DI],AX
 ADD DI, 2
 MOV AX, CS_BAK ;恢复 INTR 原中断处理程序入口段地址
 MOV ES:[DI],AX
 MOV DX,INTR_OCW1
 MOV AL, IM_BAK ;恢复 INTR 原中断屏蔽寄存器的屏蔽字
 OUT DX,AL 
 STI
 MOV AX, 4C00H ;返回到 DOS
 INT 21H
MYISR PROC NEAR ;中断处理程序 MYISR
 PUSH AX
QUERY: MOV DX,MY8259_OCW3 ;向 8259 的 OCW3 发送读取 IRR 命令
 IN AL, DX ;读出 IRR 寄存器值
 
 AND AL, 03H
 CMP AL, 01H
 JE IR0ISR ;若为 IR0 请求，跳到 IR0 处理程序
 JNE IR1ISR ;若为 IR1 请求，跳到 IR1 处理程序
 JMP OVER
IR0ISR:MOV AL, 30H
 MOV AH, 0EH
 INT 10H ;IR0 处理，显示字符串 STR0
 MOV AL, 20H
 INT 10H 
 JMP OVER
IR1ISR:MOV AL, 31H
 MOV AH, 0EH
 INT 10H ; IR1 处理，显示字符串 STR1
 MOV AL, 20H
 INT 10H
 JMP OVER
OVER: MOV DX,INTR_OCW2
 MOV AL, 20H 
 OUT DX,AL ; 向 PC 机内部 8259 发送中断结束命令
 MOV AL, 20H 
 OUT 20H, AL
 POP AX
 IRET
 
MYISR ENDP
 
CODE ENDS
 END START
